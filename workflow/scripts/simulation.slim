// set up a simple simulation
initialize() { 
	// parse out constants if using gui, otherwise these will be passed from command line
	if (exists("slimgui")) {
		defineConstant("mean", -0.03);
		defineConstant("alpha", 0.2);
		defineConstant("ID", 1);
	}

	initializeMutationType("m0", 0.5, "f", 0.0); // neutral
	initializeMutationType("m4", 0.1, "g", mean, alpha); // deleterious
	
	initializeGenomicElementType("g0", m0, 1.0); // synonymous sites 
	initializeGenomicElementType("g4", m4, 1.0); // nonsynonymous sites

	initializeRecombinationRate(1e-8); // recombination 
	initializeMutationRate(1e-6);  // mutation rate

	// Load 4-fold and 0-fold degenerate sites
	types = asInteger(readFile("types_" + asString(ID) + ".txt"));
	starts = asInteger(readFile("starts_" + asString(ID) + ".txt"));

	// remove empty space at 5' end
	starts = starts - starts[0];

	// Create distribution of synonymous and nonsynonymous sites
	for (i in 0:(length(starts) - 1)) {
		type = types[i];
		start = starts[i];
		if (type == 4){
			initializeGenomicElement(g4, start, start); 
		}

		if (type == 0){
			initializeGenomicElement(g0, start, start); 
		} 
	} 
}
 
// create a population
1 late() { 
	sim.addSubpop("p1", 1000); 
	// p1.setSelfingRate(0.25); // can't figure out how to specify this as a constant
} 

// run simulation for many generations
20000 late() { 
	//p1.outputMSSample(20, filePath = "slim.table", filterMonomorphic = T);
	
	// random sample of individuals from simulation
	g = p1.sampleIndividuals(128).genomes; 
 	
	// output sampled genomes as VCF
	g.outputVCF(filePath = "slim_" + asString(ID) + ".vcf", simplifyNucleotides=F, outputNonnucleotides=T);
}
